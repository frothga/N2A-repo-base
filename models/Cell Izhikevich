N2A.schema=3
$inherit:Compartment
 $meta
  id:166b5f39bd266700
$meta
 backend
  lems
   part:izhikevichCell,izhikevich2007Cell
  stacs
   part:10
  xyce
   part:neuron7
 gui:
  category:Base/Cell
 id:166b5f1857395797
 notes:|
  Generalized model from [izhikevich2007]. Although the original model is unitless, the book indicates the intended scale for some of the values. These imply units for the remaining parameters. You can enter values from Izhikevich publications directly in this model, as long as you append the units as they currenlty appear.
  
  The original model [izhikevich2003] is not offered as a separate class. Instead, this more general model has default values for C, k, vr and vt which make it function as the original model. When exporting to NeuroML, the appropriate class will be chosen based on whether these parameters have been modified. For Internal and C backends, there is no added cost for using the more general model, as the compiler will optimize away any unneeded terms.
  
  The other core parameters (a, b, c, d) have the default values suggested in [izhikevich2003].
$ref
 Izhikevich2003:Original model
 Izhikevich2007:page 273 gives more general model
C:1pF
 $meta
  notes:Must be 1pF for simple Izhikevich model. Values of b, d and k must be rescaled by the same order of magnitude as C. For example, if C were increased to 100pF, then the other values become: b=20nS, d=200pA, k=4(nS/mV). Alternately, rewrite V' so I/C is separate from the rest of the expression.
I:+140pA
 $meta
  backend:
   stacs
    notes:State I_app=value-140pA. If a separate value is conditioned by $init, then state I=(value@$init)-value.
    state:I_app
  notes:Must add 140pA to input current for simple Izhikevich model.
V:
 $meta:
  backend:
   stacs
    state:v
    unit:mV
 @$init:Vinit
 @fire:c
V':(k*(V-vr)*(V-vt)-u+I)/C
Vinit:c
Vspike:30mV
 $meta
  backend
   lems
    param:thresh,vpeak
a:0.02kHz
 $meta
  backend
   lems
    DL
   stacs
    param
  param
b:0.2nS
 $meta
  backend
   lems
    DL
   stacs
    param
  param
c:-65mV
 $meta
  backend
   lems
    DL
   stacs
    param
    unit:mV
  param
d:2pA
 $meta
  backend
   lems
    DL
   stacs
    param
  param
k:0.04(nS/mV)
 $meta
  backend
   lems
    DL
  notes:Must be 0.04(nS/mV) for simple Izhikevich model.
  param
u
 $meta
  backend
   lems
    param:u,U
   stacs
    state
    unit:pA
 @$init:b*Vinit
 @fire:u+d
u':a*(b*(V-vr)-u)
 $meta
  backend
   lems
    param:u',U'
vr:0mV
 $meta
  notes:Must be zero for simple Izhikevich model.
  param
vt:-125mV
 $meta
  notes:Must be -125mV for simple Izhikevich model.
  param
