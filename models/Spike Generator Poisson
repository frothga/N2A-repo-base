N2A.schema=3
$inherit:Spike Generator
 $meta
  id:166b5f18573a45ab
$meta
 backend
  lems
   part:spikeGeneratorPoisson,spikeGeneratorRefPoisson,poissonFiringSynapse,transientPoissonFiringSynapse
  stacs
   part:34
 id:166b5f18573a49ac
 notes:Exponential distribution with mean 1/averageRate.
averageRate:0Hz
 $meta
  backend
   stacs
    param:rate
  param
delay:0s
 $meta
  notes:Wait before poisson firing starts. There will be on average an additional delay of one period before the first spike is actually generated.
  param
duration:∞s
 $meta
  notes:How long poisson firing continues after the initial delay.
  param
minISI:0s
 $meta
  backend
   lems
    param:minimumISI
  notes:|
   Refractory period after a spike before another spike can be generated. Setting this nonzero changes the effect of averageRate. Actual average rate is 1/(1/averageRate - minISI).
   NeuroML does not support this parameter and delay+duration at the same time, as they generate distinct part types.
  param
nextSpikeTime:
 @$init:nextSpikeTime+minISI-(period-minISI)*log(uniform())+delay
 @fire:nextSpikeTime+minISI-(period-minISI)*log(uniform())
 @nextSpikeTime>delay+duration:∞
period:1/averageRate
 $meta
  notes:Scale for spike distribution.
  param:0
