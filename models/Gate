N2A.schema=3
$meta
 backend
  lems
   children
    HHVariables:HHVariable
    q10Settings:Q10,baseQ10Settings
    states:Kinetic State
    subGate:Gate Sub
    transitions:Kinetic Transition
   part:gate,baseGate,gateFractional,gateKS,gateHHrates,gateHHInstantaneous,gateHHratesTauInf,gateHHtauInf,gateHHratesTau,gateHHratesInf
 gui
  category:Base/Compartment
 id:166b5f185734fa2c
 notes:|
  The main component of an ion channel. To use, create a Channel and place one or more of these inside it.
  There are 3 types of Gate:
  Hodgkin-Huxley -- Rates expressed in the HH formalism. Place HHVariable objects inside this Gate.
  Kinetic -- A Markov model with state superposition, where each state allows or disallows current to flow. Use Kinetic State and Kinetic Transition.
  Fractional -- Combines several subgates which each contribute a portion of the total current. Use Gate Sub.
$up.fopen:*q^instances
Q10Scaling:*
 $meta
  backend
   lems
    param:rateScale
inf:
 @:0
 @α+β:α/(α+β)
instances:1
 $meta
  param
q:inf@q==0
 $meta
  median:1
q':(inf-q)/τ@τ
α:0Hz
 $meta
  backend
   lems
    param:alpha
β:0Hz
 $meta
  backend
   lems
    param:beta
τ:τUnscaled/Q10Scaling
 $meta
  backend
   lems
    param:tau
τUnscaled:1/(α+β)
 $meta
  backend
   lems
    param:tauUnscaled
